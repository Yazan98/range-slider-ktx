apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }

    exclude '**/R.html', '**/R.*.html', '**/index.html'
    options.encoding 'utf-8'
    options {
        addStringOption 'docencoding', 'utf-8'
        addStringOption 'charset', 'utf-8'
        links 'https://docs.oracle.com/javase/7/docs/api/'
        links 'https://d.android.com/reference'
        links 'https://developer.android.com/reference/androidx/'
    }
}

task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
    archiveClassifier.set('javadoc')
    from androidJavadoc.destinationDir

    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

task javaSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs

    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                from components.release

//                artifact androidJavadocJar
//                artifact javaSourcesJar

                groupId 'com.yazantarifi'
                version '1.0.0'
                pom {
                    name = artifactId
                    description = POM_DESCRIPTION
                    url = 'https://github.com/Yazan98/range-slider'
                    licenses {
                        license {
                            name = ''
                            url = ''
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:Yazan98/range-slider.git'
                        url = 'https://github.com/Yazan98/range-slider.git'
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "SonatypeSnapshot"

                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

                credentials {
                    username '' // change this when publish
                    password '' // change this when publish
                }
            }

            maven {
                name = "sonatype"

                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username '' // change this when publish
                    password '' // change this when publish
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
